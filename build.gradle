buildscript {
	repositories {
		mavenCentral()
		jcenter()
	}
	dependencies {
		classpath 'org.xtext:xtext-gradle-plugin:2.0.8'
	}
}

def pomXml = new XmlSlurper().parse('pom.xml')

subprojects {
	ext.xtextVersion = '2.28.0'
	repositories {
		jcenter()
	}
	
	apply plugin: 'java'
	apply plugin: 'maven-publish'
	apply plugin: 'org.xtext.xtend'
	apply from: "${rootDir}/gradle/source-layout.gradle"
	apply from: "${rootDir}/gradle/maven-deployment.gradle"
	apply plugin: 'eclipse'
	apply plugin: 'jacoco'
	
	group = 'org.contextmapper'
	version = pomXml.version
			
	sourceCompatibility = '11'
	targetCompatibility = '11'
	
	configurations.all {
		exclude group: 'asm'
	}
	
    if (!project.hasProperty('signing.secretKeyRingFile')) {
        project.ext.'signing.secretKeyRingFile' = "${rootDir}/secret-key.gpg"
    }

    // workaround for XText+Gradle build problems: https://github.com/eclipse/xtext/issues/1976#issuecomment-862141814
    configurations.all {
	    resolutionStrategy {
	        eachDependency { DependencyResolveDetails details ->
	            if (details.requested.group == 'org.eclipse.platform' && details.requested.name == 'org.eclipse.core.runtime') {
	                details.useVersion "3.19.0"
	            }
	            if (details.requested.group == 'org.eclipse.platform' && details.requested.name == 'org.eclipse.equinox.common') {
	                details.useVersion("3.13.0")
	            }
	        }
	    }
	}
}
