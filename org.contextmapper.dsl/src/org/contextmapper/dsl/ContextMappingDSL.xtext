/*
 * Copyright 2018 The Context Mapper Project Team
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *    http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

grammar org.contextmapper.dsl.ContextMappingDSL with org.contextmapper.tactic.dsl.TacticDDDLanguage

generate contextMappingDSL "http://www.contextmapper.org/dsl/ContextMappingDSL"

ContextMappingModel:
	(map = ContextMap boundedContexts += BoundedContext* subdomains += Subdomain*) | (boundedContexts += BoundedContext* subdomains += Subdomain*)
;

ContextMap:
	{ContextMap} // make sure there is always a context map
	'ContextMap'
	OPEN
		(('domainVisionStatement' '=' domainVisionStatement=STRING)? &
		('state' '=' state=ContextMapState)? &
		('type' '=' type=ContextMapType)?)
		boundedContexts += [BoundedContext]*
		relationships += Relationship*
	CLOSE
;

BoundedContext:
	'BoundedContext' name=ID ('implements' (implementedSubdomains+=[Subdomain]) ("," implementedSubdomains+=[Subdomain])*)?
	(
		OPEN
			(('type' '=' type=BoundedContextType)? &
			(('responsibilities' '=' responsibilities+=Responsibility) ("," responsibilities+=Responsibility)*)?)
			modules += Module*
		CLOSE
	)?
;

Subdomain:
	CoreDomain | SupportingDomain | GenericSubdomain
;

CoreDomain:
	'CoreDomain' name=ID
;

SupportingDomain:
	'SupportingDomain' name=ID
;

GenericSubdomain:
	'GenericSubdomain' name=ID
;

Relationship:
	SymmetricRelationship | UpstreamDownstreamRelationship
;

SymmetricRelationship:
	Partnership | SharedKernel
;

Partnership:
	'Partnership'
	OPEN
		participant1 = [BoundedContext]
		participant2 = [BoundedContext]
	CLOSE
;

SharedKernel:
	'Shared' 'Kernel'
	OPEN
		participant1 = [BoundedContext]
		participant2 = [BoundedContext]
	CLOSE
;

UpstreamDownstreamRelationship:
	CustomerSupplierRelationship |
	'Upstream' 'Downstream'
	OPEN
		upstream = UpstreamContext
		downstream = DownstreamContext
	CLOSE
;

CustomerSupplierRelationship:
	'Customer' 'Supplier'
	OPEN
		upstream = UpstreamContext
		downstream = DownstreamContext
	CLOSE
;

UpstreamContext:
	context = [BoundedContext] 'as' 'Upstream'
	roles += UpstreamRoles*
;

DownstreamContext:
	context = [BoundedContext] 'as' 'Downstream'
	roles += DownstreamRoles*
;

Responsibility:
	name=ID
;

enum UpstreamRoles:
	PUBLISHED_LANGUAGE | OPEN_HOST_SERVICE
;

enum DownstreamRoles:
	ANTICORRUPTION_LAYER | CONFORMIST
;

enum ContextMapState:
	AS_IS | TO_BE
;

enum ContextMapType:
	ORGANIGRAM | SYSTEM_LANDSCAPE
;

enum BoundedContextType:
	FEATURE | APPLICATION | SYSTEM | TEAM
;

// define terminals
terminal OPEN: '{';
terminal CLOSE: '}';
